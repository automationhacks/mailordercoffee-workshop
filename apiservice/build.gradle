apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 31


    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
    flavorDimensions "providerVersion", "dataType"

    productFlavors {
        simpledata {
            dimension "providerVersion"
        }
        retrofitdata {
            dimension "providerVersion"
        }
        local {
            dimension "dataType"
        }
        remote {
            dimension "dataType"
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        def types = variant.buildType.name
        // To check for a certain build type, use variant.buildType.name == "<buildType>"
        if (names.contains("retrofitdata") && names.contains("local") || types.contains("release")) {
            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(true)
        }
    }
    buildToolsVersion '30.0.3'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.3.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.2-native-mt'
    implementation 'androidx.core:core-ktx:1.7.0-rc01'
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    implementation('org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2-native-mt')
    androidTestUtil 'androidx.test:orchestrator:1.4.0'
}
repositories {
    mavenCentral()
}
